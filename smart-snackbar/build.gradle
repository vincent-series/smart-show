plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'maven-publish'
    id 'signing'
}
android {
    compileSdk 31

    defaultConfig {
        minSdk 17
        targetSdk 31
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

dependencies {
    compileOnly "androidx.core:core-ktx:${dependencyLibVersion.core_ktx}"
    compileOnly "androidx.appcompat:appcompat:${dependencyLibVersion.appcompat}"
    api "io.github.vincent-series:common:1.0.4"
    compileOnly 'com.google.android.material:material:1.5.0'
}
afterEvaluate {
    publishing {
        def stagingVersion = "4.0.3"
        def snapshotsVersion = "4.0.1-1.0.1-SNAPSHOT"
        def uploadVersion = stagingVersion
        publications {
            maven(MavenPublication) {
                from components.release
                groupId = 'io.github.vincent-series'
                artifactId = 'smart-snackbar'
                version = uploadVersion

                pom {
                    name = 'smart-snackbar'
                    description = 'snackbar that can show at top and custom style developed by zhiqiang.zhu.'
                    url = 'https://github.com/vincent-series/smart-show'

                    licenses {
                        license {
                            name = 'The Apache License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }

                    developers {
                        developer {
                            id = 'zhiqiang.zhu'
                            name = 'zhiqiang.zhu'
                            email = 'coder_zzq@aliyun.com'
                        }
                    }
                    scm {
                        connection = 'scm:git:https://github.com/vincent-series/smart-show.git'
                        developerConnection = 'scm:git:https://github.com/vincent-series/smart-show.git'
                        url = 'https://github.com/vincent-series/smart-show'
                    }
                }

                artifact generateSourcesJar
            }
        }

        repositories {
            maven {
                url = uploadVersion == stagingVersion ?
                        'https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/'
                        : 'https://s01.oss.sonatype.org/content/repositories/snapshots/'
                credentials {
                    username repo_user_name
                    password repo_password
                }
            }
        }

        signing {
            sign publishing.publications.maven
        }
    }
}

task generateSourcesJar(type: Jar) {
    group = 'jar'
    archiveClassifier.set("sources")
    from("src/main/java")
}